pkgname=sdl-gfx
pkgver=2.0.27
pkgrel=3
pkgdesc="SDL graphics drawing primitives and other support functions"
arch=('mips')
url="https://www.ferzkopp.net/wordpress/2016/01/02/sdl_gfx-sdl2_gfx/"
license=('ZLIB')
depends=('sdl' 'freetype2')
makedepends=()
optdepends=()
source=("https://sourceforge.net/projects/sdlgfx/files/SDL_gfx-${pkgver}.tar.gz")
sha256sums=('dfb15ac5f8ce7a4952dc12d2aed9747518c5e6b335c0e31636d23f93c630f419')

prepare() {
    cd "SDL_gfx-$pkgver"
    wget -O config.sub "http://git.savannah.gnu.org/gitweb/?p=autoconf.git;a=blob_plain;f=build-aux/config.sub"
    sed -i 's#@prefix@#${PSPDEV}/psp#' SDL_gfx.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' SDL_gfx.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' SDL_gfx.pc.in
    sed -i 's#@includedir@#${prefix}/include#' SDL_gfx.pc.in
}

build() {
    cd "SDL_gfx-$pkgver"
    LDFLAGS="-L$(psp-config -p)/lib"  \
    ./configure --host psp  --with-sdl-prefix=$(psp-config --psp-prefix) --prefix=/psp --disable-mmx --disable-shared
    make
}

package() {
    cd "SDL_gfx-$pkgver"
    make DESTDIR="$pkgdir/" install

    rm "${pkgdir}/psp/lib/libSDL_gfx.la"

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}

